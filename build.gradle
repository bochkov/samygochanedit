import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

group = 'com.sb'
version = '0.6'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.sergeybochkov.com/releases"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'com.miglayout:miglayout-swing:11.3'
    implementation 'com.formdev:flatlaf:3.4'
    implementation 'com.formdev:flatlaf-fonts-inter:4.0'
    implementation 'com.sb:bdev-rt:1.0.4.RELEASE'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootRun') {
    jvmArgs += [
            '-Xms64m',
            '-Xmx128m',
            '-Dfile.encoding=UTF-8'
    ]
    if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
        jvmArgs += [
                '--add-opens', 'java.base/java.util=ALL-UNNAMED',
        ]
    }
    if (Os.isFamily(Os.FAMILY_MAC)) {
        if (JavaVersion.current() >= JavaVersion.VERSION_17) {
            jvmArgs += ['-Dsun.java2d.metal=true']
        }
        jvmArgs += ['-Xdock:name=SamyGo Channel Editor']
    }
}