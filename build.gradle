import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.sb'
version = '0.5'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'com.miglayout:miglayout-swing:11.0'
    implementation 'com.formdev:flatlaf:2.0.1'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootRun') {
    jvmArgs += [
            '-Xms64m',
            '-Xmx128m',
            '-Dfile.encoding=UTF-8'
    ]
    if (targetCompatibility.isCompatibleWith(JavaVersion.VERSION_1_9)) {
        jvmArgs += [
                '--add-opens', 'java.base/java.util=ALL-UNNAMED',
        ]
    }
    if (Os.isFamily(Os.FAMILY_MAC)) {
        if (targetCompatibility.isCompatibleWith(JavaVersion.VERSION_17)) {
            jvmArgs += ['-Dsun.java2d.metal=true']
        }
        jvmArgs += ['-Xdock:name=SamyGo Channel Editor']
    }
}